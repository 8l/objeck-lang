use IO;
use XML;
use Struct;

bundle Default {
	class WsdlTest {
		function : Main(args : String[]) ~ Nil {
			if(args->Size() > 0) {
				Run(args[0]);
			};
		}
				
		function : Run(name : String) ~ Nil {
			parser := XmlParser->New(FileReader->ReadFile(name));
			if(parser->Parse()) {
				results := parser->FindElements("/wsdl:definitions/wsdl:types");
				if(results->Size() > 0) {
					"===== TYPES ====="->PrintLine();
					types := results->Get(0)->As(XmlElement);
					elem_name := types->GetName();
					elem_name->Append("/xsd:schema/xsd:element");
					results := types->FindElements(elem_name);
					each(i : results) {	
						elem := results->Get(i)->As(XmlElement);
						type_name := elem->GetAttribute("name");
						elem_type := elem->GetChild(0)->GetChild(0);
						type_type := elem_type->GetName();
						
						Console->Print("type: name=")->Print(type_name)->Print(", ctype=")
							->PrintLine(type_type);
						
						elems := elem_type->GetChildren("xsd:element");
						each(j : elems) {	
							elem := elems->Get(j)->As(XmlElement);
							Console->Print("\tname=")->Print(elem->GetAttribute("name"))
								->Print(", type=")->PrintLine(elem->GetAttribute("type"));
						};
						"----"->PrintLine();
					};
				};
				
				"===== MESSAGES ====="->PrintLine();
				results := parser->FindElements("/wsdl:definitions/wsdl:message");
				messages := StringMap->New();
				each(i : results) {
					message := results->Get(i)->As(XmlElement);
					message_name := message->GetAttribute("name");
					Console->Print("message: name=")->PrintLine(message_name);
					parts := message->GetChildren();
					each(j : parts) {
						part := parts->Get(j)->As(XmlElement);
						part_name := part->GetAttribute("name");
						part_elem := part->GetAttribute("element");
						Console->Print("\tpart: name=")->Print(part_name)->Print(", element=")
							->PrintLine(part_elem);
						messages->Insert(part_elem, message_name);
					};
				};
				Console->Print("message count: ")->PrintLine(messages->Size());
				
				results := parser->FindElements("/wsdl:definitions/wsdl:portType");
				ports := StringMap->New();		
				if(results->Size() > 0) {
					"===== PORTS ====="->PrintLine();
					port := results->Get(0)->As(XmlElement);
					port_name := port->GetAttribute("name");
					Console->Print("port: name=")->PrintLine(port_name);
					operations := port->GetChildren("wsdl:operation");
					each(i : operations) {
						operation := operations->Get(i)->As(XmlElement);
						operation_name := operation->GetAttribute("name");
						Console->Print("\toperation: name=")->PrintLine(operation_name);
						p := Port->New(operation_name);
						ios := operation->GetChildren();
						each(j : ios) {
							io := ios->Get(j)->As(XmlElement);
							Console->Print("\t\tio: type=")->Print(io->GetName())
								->Print(", message=")->PrintLine(io->GetAttribute("message"));
							p->AddOperation(io->GetName(), io->GetAttribute("message"));
						};
						ports->Insert(operation_name, p);
					};
				};
				Console->Print("port count: ")->PrintLine(ports->Size());
			};
		}
	}
	
	class Port {
		@name : String;
		@operations : StringMap;
		
		New(name : String) {
			@name := name;
			@operations := StringMap->New();
		}
		
		method : public : AddOperation(name: String, value: String) ~ Nil {
			@operations->Insert(name, value);
		}
	}
}
