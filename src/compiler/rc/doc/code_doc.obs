use System.IO;
use System.IO.File;
use Collection;

class Parser {
	function : Main(args : String[]) ~ Nil {
		if(args->Size() = 1) {
			Parse(args[0]);
		};
	}
	
	function : native : Parse(file : String) ~ Nil {
		capture := 0;
		block := "";
		line_num := 0;
		comment_state := 0;							
		reader := FileReader->New(file);
		class_doc : ClassBlock;
		while(<>reader->IsEOF()) {
			line := reader->ReadString();
			line_num += 1;
			
			i := 0;	
			while(i < line->Size()) {		
				# skip whitespace
				while((line->Get(i) = ' ' | line->Get(i) = '\t') & i < line->Size()) {
					i += 1;
				};
				
				# parse the line
				if(i < line->Size()) {
					# word
					if(line->Get(i)->IsChar() = true) {
						string := "";
						while(line->Get(i)->IsChar() = true | line->Get(i) = '.') {
							string->Append(line->Get(i));
							i += 1;
						};
						
						if(capture = 2 & string->Equals("class")) {
							capture := 3;
						}
						else if(capture = 2 & (string->Equals("function") | string->Equals("method"))) {
							capture := 4;
						};						
					}
					# other
					else {
						select(line->Get(i)) {
							label '{': {
								if(capture = 3) {
									block->Append(line);
									block->Append('\n');
									class_doc := ClassBlock->New(block);								
									block := "";
"--- Class ---"->PrintLine();									
								}
								else if(capture = 4) {
									block->Append(line);
									block->Append('\n');
									if(class_doc <> Nil) {
										class_doc->AddFunction(block);
										block := "";
"--- Func ---"->PrintLine();										
									};
								};
								capture := 0;
								comment_state := 0;
							}
			
							label '#': {
								if(comment_state = 2) {
									capture := 2;
									comment_state := 0;
								}
								else if(comment_state = 0) {
									comment_state := 1;
								};
							}

							label '~': {
								if(comment_state = 1) {
									capture := 1;
									comment_state := 0;
								}
								else if(comment_state = 0) {
									comment_state := 2;
								};
							}
					
							other: {
								comment_state := 0;
							}
						};						
					};	
				};
				i += 1;
			};				
			
			if(capture > 0) {
				block->Append(line);
				block->Append('\n');
			};
		};
		reader->Close();
	}
}

class DocBlock {
	@buffer : String;
	@buffer_index : Int;
	@cur_char : Char;
	@nxt_char : Char;
	@parsed : Bool;
	
	New(buffer : String) {
		@buffer := buffer;
		@buffer_index := 0;
		@parsed := false;
		NextChar();
	}
	
	method : NextChar() ~ Nil {
		if(@buffer_index < @buffer->Size()) {
			@cur_char := @buffer->Get(@buffer_index);
			@buffer_index += 1;
			if(@buffer_index < @buffer->Size()) {
				@nxt_char := @buffer->Get(@buffer_index);
			}
			else {
				@nxt_char := '\0';
			};
		}
		else {
			@cur_char := '\0';
			@nxt_char := '\0';
		};
	}
	
	method : ParseWhiteSpace() ~ Nil {
		while(@buffer_index < @buffer->Size() & (@cur_char = ' ' | @cur_char = '\t' | @cur_char = '\r' | @cur_char = '\n')) {
			NextChar();
		};
	}

	method : MatchCommentStart() ~ Bool {
		ParseWhiteSpace();
		match := @cur_char = '#' & @nxt_char = '~';
		if(match) {
			NextChar();	NextChar();
			return true;
		};
		
		return false;
	}

	method : MatchCommentEnd() ~ Bool {
		ParseWhiteSpace();
		match := @cur_char = '~' & @nxt_char = '#';
		if(match) {
			NextChar();	NextChar();
			return true;
		};
		
		return false;
	}
	
	method : MatchChar(char : Char) ~ Bool {
		ParseWhiteSpace();
		
		match := @cur_char = char;
		if(match) {
			NextChar();
			return true;
		};
		
		return false;
	}

	method : MatchIdent(ident : String) ~ Bool {
		return ParseIdent()->Equals(ident);
	}

	method : ParseIdent() ~ String {
		ParseWhiteSpace();

		ident := "";
		done := false;
		while(<>done) {
			select(@cur_char) {
				label ' ':
				label '\r':
				label '\n':
				label '{':
				label ':':
				label ',':
				label '(':
				label ')':
				label '~':
				label '#':
				label '\0': {
					done := true;
				}				
				other: {
					ident->Append(@cur_char);
					NextChar();
				}
			};
		};
		
		return ident->Trim();
	}
	
	method : ParseDesc() ~ String {
		ParseWhiteSpace();
		
		ident := "";
		done := false;
		while(<>done) {
			select(@cur_char) {
				label ' ':
				label '\t':
				label '\r':
				label '\n': {
					ParseWhiteSpace();
					ident->Append(' ');
				}
#~
				label '#': {
					ident->Append(' ');
					NextChar();
				}
~#
				label '{':
				label ':':
				label '(':
				label ')':
				label '~':
				label '@':
				label '\0': {
					done := true;
				}				
				other: {
					ident->Append(@cur_char);
					NextChar();
				}
			};
		};
		
		return ident->Trim();
	}

	method : WasParsed() ~ Bool {
		return @parsed;
	}
}

class ClassBlock from DocBlock {
	@name : String;
	@funcs : StringMap;
	
	New(buffer : String) {
		Parent(buffer);
		@funcs := StringMap->New();
		@parsed := Parse();
	}

	method : public : GetName() ~ String {
		return @name;
	}

	method : public : AddFunction(block : String) ~ Nil {
		func := FuncBlock->New(block);
		if(func->WasParsed()) {
			@funcs->Insert(func->GetName(), func);
		};
	}

	method : native : Parse() ~ Bool {
		if(<>MatchCommentStart()) {
			return false;
		};
		desc := ParseDesc();
		if(<>MatchCommentEnd()) {
			return false;
		};

		if(<>MatchIdent("class")) {
			return false;
		};

		@name := ParseIdent();
		if(@name->Size() > 0) {
			return true;
		};
		
		return false;
	}
}

class FuncBlock from DocBlock {
	@name : String;
	@desc : String;
	@tags : StringHash;
	@return_type : DocType;
	@func_types : List;
	@is_func : Bool;
	@is_private : Bool;
	@is_native : Bool;
	
	New(buffer : String) {
		Parent(buffer);
		@name := "";
		@desc := "";
		@tags := StringHash->New();
		@func_types := List->New();
		
		@parsed := Parse();
	}
	
	method : public : GetName() ~ String {
		return @name;
	}

	method : public : GetDesc() ~ String {
		return @desc;
	}

	method : public : GetReturn() ~ DocType {
		return @return_type;
	}
	
	method : native : Parse() ~ Bool {
		if(<>MatchCommentStart()) {
			return false;
		};
		@desc := ParseDesc();

		# parse tags		
		are_tags := true;
		while(@cur_char <> '\0' & are_tags) {
			type := ParseIdent();
			if(type->Size() > 0) {
				if(type->Equals("@param")) {
					tag_name := ParseIdent();
					if(tag_name->Size() = 0) {
						return false;
					};

					tag_desc := ParseDesc();
					if(tag_desc->Size() = 0) {
						return false;
					};
					tag := DocTag->New(tag_name, tag_desc);
					@tags->Insert(tag->GetName(), tag);
				}
				else if(type->Equals("@return")) {
					tag_desc := ParseDesc();
					if(tag_desc->Size() = 0) {
						return false;
					};
					tag := DocTag->New(tag_desc);
				}
				else {
					return false;
				};
			}
			else {
				are_tags := false;
			};
		};

		if(<>MatchCommentEnd()) {
			return false;
		};
		
		# parse function
		type := ParseIdent();
		if(type->Size() > 0) {
			if(type->Equals("function")) {
				@is_fun := true;
			}
			else if(type->Equals("method")) {
				@is_fun := false;
			}
			else {
				return false;
			};
		};

		while(MatchChar(':')) {
			meta := ParseIdent();
			if(meta->Size() > 0) {
				if(meta->Equals("public")) {
					@is_public := true;
				}
				else if(meta->Equals("private")) {
					@is_public := false;
				}
				else if(meta->Equals("native")) {
					@is_native := true;
				}
				else {
					@name := meta;
				};
			};
		};
		
		if(@name = Nil | <>MatchChar('(')) {
			return false;
		};
		
		# function parameters
		do {
			param_name := ParseIdent();
			if(param_name->Size() = 0) {
				return false;
			};
IO.Console->Print("parsed name='")->Print(param_name)->PrintLine("'");
		
			if(<>MatchChar(':')) {
				return false;
			};
		
			type_name := "";
			type_param := ParseType(type_name);
			if(type_param <> Nil) {
IO.Console->Print("\tparsed type='")->Print(type_name)->PrintLine("'");
				MatchChar(',');
			};
		}
		while(type_param<> Nil & @cur_char <> ')');
		
		if(<>MatchChar(')')) {
			return false;
		};

		if(<>MatchChar('~')) {
			return false;
		};

		# return type
		rtrn_name := ParseIdent();
		if(rtrn_name->Size() = 0) {
			return false;
		};
			
		# TODO: make this a hash	
		if(rtrn_name->Equals("Int")) {
			@return_type := DocType->New(rtrn_name, DocDataType->INT);
		}
		else if(rtrn_name->Equals("Float")) {
			@return_type := DocType->New(rtrn_name, DocDataType->FLOAT);
		}
		else if(rtrn_name->Equals("Nil")) {
			@return_type := DocType->New(rtrn_name, DocDataType->NIL);
		}
		else {
			@return_type := DocType->New(rtrn_name, DocDataType->CLASS);
		}; 		

		return true;
	}
	
	method : ParseType(type_name : String) ~ DocType {
		if(MatchChar('(')) {
			type_name->Append('(');

			if(ParseFuncType(type_name)) {
				return DocType->New(type_name, DocDataType->FUNC);
			}
			else {
				return Nil;
			};
		}
		else {
			ident := ParseIdent();
			if(ident->Size() = 0) {
				return Nil;
			};
			type_name->Append(ident);
		
			# TODO: make this a hash	
			if(type_name->Equals("Int")) {
				return DocType->New(type_name, DocDataType->INT);
			}
			else if(type_name->Equals("Float")) {
				return DocType->New(type_name, DocDataType->FLOAT);
			}
			else {
				return DocType->New(type_name, DocDataType->CLASS);
			};
		};
	}
	
	method : ParseFuncType(type_name : String) ~ Bool {
		ident := ParseIdent();
		while(ident->Size() > 0) {
			type_name->Append(ident);
			if(MatchChar(',')) {
				type_name->Append(',');
			};
			ident := ParseIdent();
		};

		if(<>MatchChar(')')) {
			return false;
		};
		type_name->Append(')');

		if(<>MatchChar('~')) {
			return false;
		};
		type_name->Append('~');

		ident := ParseIdent();
		if(ident->Size() = 0) {
			return false;
		};
		type_name->Append(ident);
		
		return true;
	}
}

enum DocTagType {
	PARAM,
	RETURN
}	

class DocTag {
	@type : DocTagType;
	@name : String;
	@desc : String;

	New(name : String, desc : String) {
		@type := DocTagType->PARAM;
		@name := name;
		@desc := desc;
	}

	New(desc : String) {
		@type := DocTagType->RETURN;
		@name := "";
		@desc := desc;
	}
	
	method : public : GetType() ~ DocTagType {
		return @type;
	}

	method : public : GetName() ~ String {
		return @name;
	}

	method : public : GetDesc() ~ String {
		return @desc;
	}
}

enum DocDataType {
	BYTE,
	CHAR,
	INT,
	FLOAT,
	CLASS,
	FUNC,
	NIL
}

class DocType {
	@type : DocDataType;
	@name : String;	
	
	New(name : String, type : DocDataType) {
		@type := type;
		@name := name;
	}

	method : public : GetName() ~ String {
		return @name;
	}

	method : public : GetType() ~ DocDataType {
		return @type;
	}
}

