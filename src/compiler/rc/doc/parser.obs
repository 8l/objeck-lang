use System.IO;
use System.IO.File;
use Collection;

class DocParser {
	@file : String;
	@classes : Vector;
	@enums : Vector;
	
	New(file : String) {
		@file := file;	
		@classes := Vector->New();
		@enums := Vector->New();
	}
	
	method : public : GetClasses() ~ Vector {
		return @classes;
	}

	method : public : GetEnums() ~ Vector {
		return @enums;
	}
	
	method : public : native : Parse() ~ Bool {
		capture := 0;
		block := "";
		line_num := 0;
		comment_state := 0;							
		reader := FileReader->New(@file);
		
		
		class_doc : ClassBlock;
		while(<>reader->IsEOF()) {
			line := reader->ReadString();
			line_num += 1;
			
			i := 0;	
			while(i < line->Size()) {		
				# skip whitespace
				while((line->Get(i) = ' ' | line->Get(i) = '\t') & i < line->Size()) {
					i += 1;
				};
				
				# parse the line
				if(i < line->Size()) {
					# word
					if(line->Get(i)->IsChar() = true) {
						string := "";
						while(line->Get(i)->IsChar() = true | line->Get(i) = '.') {
							string->Append(line->Get(i));
							i += 1;
						};
						
						if(capture = 2 & string->Equals("class")) {
							capture := 3;
						}
						else if(capture = 2 & string->Equals("enum")) {
							capture := 4;
						}
						else if(capture = 2 & (string->Equals("function") | string->Equals("method") | string->Equals("New"))) {
							capture := 5;
						}
						else if(capture = 2 & string->Equals("enum")) {
							capture := 6;
						};						
					}
					# other
					else {
						select(line->Get(i)) {
							label '{': {
								if(capture = 3) {
									block->Append(line);
									block->Append('\n');
									class_doc := ClassBlock->New(block);
									if(class_doc <> Nil) {
										@classes->AddBack(class_doc);
									};								
									block := "";
								}
								else if(capture = 4) {
									block->Append(line);
									block->Append('\n');
									enum_doc := EnumBlock->New(block);
									if(enum_doc <> Nil) {
block->PrintLine();
										@enums->AddBack(enum_doc);
									};								
									block := "";
								}
								else if(capture = 5) {
									block->Append(line);
									block->Append('\n');
									if(class_doc <> Nil) {
										class_doc->AddFunction(block);
										block := "";
									};
								}
								else if(capture = 6) {
									block->Append(line);
									block->Append('\n');
									if(class_doc <> Nil) {
block->PrintLine();
										class_doc->AddEnum(block);
										block := "";
									};
								};
								capture := 0;
								comment_state := 0;
							}
			
							label '#': {
								if(comment_state = 2) {
									capture := 2;
									comment_state := 0;
								}
								else if(comment_state = 0) {
									comment_state := 1;
								};
							}

							label '~': {
								if(comment_state = 1) {
									capture := 1;
									comment_state := 0;
								}
								else if(comment_state = 0) {
									comment_state := 2;
								};
							}
					
							other: {
								comment_state := 0;
							}
						};						
					};	
				};
				i += 1;
			};				
			
			if(capture > 0) {
				block->Append(line);
				block->Append('\n');
			};
		};
		
		leaving {
			if(reader <> Nil) {
				reader->Close();
			};
		};

		return true;	
	}
}

class DocBlock {
	@buffer : String;
	@buffer_index : Int;
	@cur_char : Char;
	@nxt_char : Char;
	@parsed : Bool;
	
	New(buffer : String) {
		@buffer := buffer;
		@buffer_index := 0;
		@parsed := false;
		NextChar();
	}
	
	method : NextChar() ~ Nil {
		if(@buffer_index < @buffer->Size()) {
			@cur_char := @buffer->Get(@buffer_index);
			@buffer_index += 1;
			if(@buffer_index < @buffer->Size()) {
				@nxt_char := @buffer->Get(@buffer_index);
			}
			else {
				@nxt_char := '\0';
			};
		}
		else {
			@cur_char := '\0';
			@nxt_char := '\0';
		};
	}
	
	method : ParseWhiteSpace() ~ Nil {
		while(@buffer_index < @buffer->Size() & (@cur_char = ' ' | @cur_char = '\t' | 
			@cur_char = '\r' | @cur_char = '\n')) {
			NextChar();
		};
	}

	method : MatchCommentStart() ~ Bool {
		ParseWhiteSpace();
		match := @cur_char = '#' & @nxt_char = '~';
		if(match) {
			NextChar();	NextChar();
			return true;
		};
		
		return false;
	}

	method : MatchCommentEnd() ~ Bool {
		ParseWhiteSpace();
		match := @cur_char = '~' & @nxt_char = '#';
		if(match) {
			NextChar();	NextChar();
			return true;
		};
		
		return false;
	}
	
	method : MatchChar(char : Char) ~ Bool {
		ParseWhiteSpace();
		
		match := @cur_char = char;
		if(match) {
			NextChar();
			return true;
		};
		
		return false;
	}

	method : MatchIdent(ident : String) ~ Bool {
		return ParseIdent()->Equals(ident);
	}

	method : ParseIdent() ~ String {
		ParseWhiteSpace();

		ident := "";
		done := false;
		while(<>done) {
			select(@cur_char) {
				label ' ':
				label '\r':
				label '\n':
				label '{':
				label ':':
				label ',':
				label '(':
				label ')':
				label '~':
				label '#':
				label '\0': {
					done := true;
				}				
				other: {
					ident->Append(@cur_char);
					NextChar();
				}
			};
		};
		
		return ident->Trim();
	}
	
	method : ParseDesc() ~ String {
		ParseWhiteSpace();
		
		ident := "";
		done := false;
		while(<>done) {
			select(@cur_char) {
				label ' ':
				label '\t':
				label '\r':
				label '\n': {
					ParseWhiteSpace();
					ident->Append(' ');
				}
#~
				label '#': {
					ident->Append(' ');
					NextChar();
				}
~#
				label '{':
				label ':':
				label '(':
				label ')':
				label '~':
				label '@':
				label '\0': {
					done := true;
				}				
				other: {
					ident->Append(@cur_char);
					NextChar();
				}
			};
		};
		
		return ident->Trim();
	}

	method : WasParsed() ~ Bool {
		return @parsed;
	}
}

class EnumBlock from DocBlock {
	@name : String;
	@desc : String;
	@items : Vector;
	
	New(buffer : String) {
		Parent(buffer);
		@items := Vector->New();
		@parsed := Parse();
	}

	method : public : GetName() ~ String {
		return @name;
	}

	method : public : GetItems() ~ Vector {
		return @items;
	}

	method : native : Parse() ~ Bool {
		return false;
	}
}

class ClassBlock from DocBlock {
	@name : String;
	@desc : String;
	@funcs : StringMap;
	
	New(buffer : String) {
		Parent(buffer);
		@funcs := StringMap->New();
		@parsed := Parse();
	}

	method : public : GetName() ~ String {
		return @name;
	}

	method : public : GetDesc() ~ String {
		return @desc;
	}

	method : public : AddEnum(block : String) ~ Nil {
	}

	method : public : AddFunction(block : String) ~ Nil {
		func := FuncBlock->New(block);
		if(func->WasParsed()) {
			funcs := @funcs->Find(func->GetName())->As(Vector);
			if(funcs = Nil) {
				funcs := Vector->New();
				@funcs->Insert(func->GetName(), funcs);
			};
			funcs->AddBack(func);
		};
	}

	method : public : GetFunction(name : String) ~ Vector {
		return @funcs->Find(name)->As(Vector);
	}

	method : public : GetFunctions() ~ Vector {
		all_funcs := Vector->New();
		
		func_names := @funcs->GetKeys();
		each(i : func_names) {
			funcs := @funcs->Find(func_names->Get(i)->As(String))->As(Vector);
			each(j : funcs) {
				all_funcs->AddBack(funcs->Get(j));
			};
		};
				
		return all_funcs;
	}

	method : native : Parse() ~ Bool {
		if(<>MatchCommentStart()) {
			return false;
		};
		@desc := ParseDesc();
		if(<>MatchCommentEnd()) {
			return false;
		};

		if(<>MatchIdent("class")) {
			return false;
		};

		@name := ParseIdent();
		if(@name->Size() > 0) {
			return true;
		};
		
		return false;
	}
}

class FuncBlock from DocBlock {
	@name : String;
	@signature : String;
	@desc : String;
	@tags : StringHash;
	@return_type : DocType;
	@return_tag : DocTag;
	@func_types : Vector;
	@is_func : Bool;
	@is_private : Bool;
	@is_native : Bool;
	@is_new : Bool;
	@types : static : StringHash;
	
	New(buffer : String) {
		Parent(buffer);
		Init();
		
		@name := "";
		@desc := "";
		@tags := StringHash->New();
		@func_types := Vector->New();
		@parsed := Parse();
	}
	
	function : Init() ~ Nil {
		if(@types = Nil) {
			@types := StringHash->New();
			@types->Insert("Byte", IntHolder->New(DocType->Type->BYTE->As(Int)));
			@types->Insert("Char", IntHolder->New(DocType->Type->CHAR->As(Int)));
			@types->Insert("Int", IntHolder->New(DocType->Type->INT->As(Int)));
			@types->Insert("Float", IntHolder->New(DocType->Type->FLOAT->As(Int)));
			@types->Insert("Nil", IntHolder->New(DocType->Type->NIL->As(Int)));
		};
	}
	
	method : public : GetName() ~ String {
		return @name;
	}

	method : public : GetSignature() ~ String {
		if(@signature = Nil) {
			@signature := "";
			
			if(@is_new) {
				@signature->Append("New");
			}
			else {
				if(@is_func) {
					@signature->Append("function : ");
				}
				else {
					@signature->Append("method : ");
				};
				
				if(@is_private) {
					@signature->Append("private : ");
				}
				else {
					@signature->Append("public : ");
				};
		
				if(@is_native) {
					@signature->Append("native : ");
				};
				@signature->Append(@name);
			};
			@signature->Append('(');

			each(i : @func_types) {
				func_type := @func_types->Get(i)->As(DocType);
				@signature->Append(func_type->GetName());
				@signature->Append(':');
				@signature->Append(func_type->GetTypeName());
			
				if(i + 1 < @func_types->Size()) {
					@signature->Append(", ");
				};
			};
		
			if(@is_new) {
				@signature->Append(')');
			}
			else {
				@signature->Append(") ~ ");
				@signature->Append(@return_type->GetName());
			};
		};
		
		return @signature;
	}

	method : public : GetReturn() ~ DocTag {
		return @return_tag;
	}

	method : public : GetDesc() ~ String {
		return @desc;
	}

	method : public : GetParamTags() ~ StringHash {
		return @tags;
	}
	
	method : public : GetParams() ~ Vector {
		return @func_types;
	}
	
	method : native : Parse() ~ Bool {
		if(<>MatchCommentStart()) {
			return false;
		};
		@desc := ParseDesc();	

		# parse tags		
		are_tags := true;
		while(@cur_char <> '\0' & are_tags) {
			type := ParseIdent();
			if(type->Size() > 0) {
				if(type->Equals("@param")) {
					tag_name := ParseIdent();
					if(tag_name->Size() = 0) {
						return false;
					};

					tag_desc := ParseDesc();
					if(tag_desc->Size() = 0) {
						return false;
					};
					tag := DocTag->New(tag_name, tag_desc);
					@tags->Insert(tag->GetName(), tag);
				}
				else if(type->Equals("@return")) {
					tag_desc := ParseDesc();
					if(tag_desc->Size() = 0) {
						return false;
					};
					@return_tag := DocTag->New(tag_desc);
				}
				else {
					return false;
				};
			}
			else {
				are_tags := false;
			};
		};

		if(<>MatchCommentEnd()) {
			return false;
		};
		
		# parse function
		func_type := ParseIdent();
		if(func_type->Size() > 0) {
			if(func_type->Equals("function")) {
				@is_func := true;
			}
			else if(func_type->Equals("method")) {
				@is_func := false;
			}
			else if(func_type->Equals("New")) {
				@is_new := true;
				@is_func := false;
			}
			else {
				return false;
			};
		};

		if(@is_new) {
			@name := "New";
		}
		else {
			while(MatchChar(':')) {
				meta := ParseIdent();
				if(meta->Size() > 0) {
					if(meta->Equals("public")) {
						@is_public := true;
					}
					else if(meta->Equals("private")) {
						@is_public := false;
					}
					else if(meta->Equals("native")) {
						@is_native := true;
					}
					else {
						@name := meta;
					};
				};
			};
		};
		
		if(@name = Nil | <>MatchChar('(')) {
			return false;
		};
		
		# function parameters
		while(@cur_char <> ')') {
			param_name := ParseIdent();
			if(param_name->Size() = 0) {
				return false;
			};
		
			if(<>MatchChar(':')) {
				return false;
			};
		
			type_name := "";
			type_param := ParseType(param_name, type_name);
			if(type_param <> Nil) {
				@func_types->AddBack(type_param);
				MatchChar(',');
			};
		};
		
		if(<>MatchChar(')')) {
			return false;
		};
		
		if(<>@is_new) {
			if(<>MatchChar('~')) {
				return false;
			};

			# return type
			rtrn_name := ParseIdent();
			if(rtrn_name->Size() = 0) {
				return false;
			};
		
			rtrn_type := @types->Find(rtrn_name)->As(IntHolder);
			if(rtrn_type <> Nil) {
				@return_type := DocType->New(rtrn_name, "", rtrn_type->Get()->As(DocType->Type));
			}
			else {
				@return_type := DocType->New(rtrn_name, "", DocType->Type->CLASS);
			};
		};
		
		return true;
	}
	
	method : ParseType(param_name : String, type_name : String) ~ DocType {
		if(MatchChar('(')) {
			type_name->Append('(');

			if(ParseFuncType(type_name)) {
				return DocType->New(param_name, type_name, DocType->Type->FUNC);
			}
			else {
				return Nil;
			};
		}
		else {
			ident := ParseIdent();
			if(ident->Size() = 0) {
				return Nil;
			};
			type_name->Append(ident);
		
			param_type := @types->Find(type_name)->As(IntHolder);
			if(param_type <> Nil) {
				return DocType->New(param_name, type_name, param_type->Get()->As(DocType->Type));
			}
			else {
				return DocType->New(param_name, type_name, DocType->Type->CLASS);
			};
		};
	}
	
	method : ParseFuncType(type_name : String) ~ Bool {
		ident := ParseIdent();
		while(ident->Size() > 0) {
			type_name->Append(ident);
			if(MatchChar(',')) {
				type_name->Append(',');
			};
			ident := ParseIdent();
		};

		if(<>MatchChar(')')) {
			return false;
		};
		type_name->Append(')');

		if(<>MatchChar('~')) {
			return false;
		};
		type_name->Append('~');

		ident := ParseIdent();
		if(ident->Size() = 0) {
			return false;
		};
		type_name->Append(ident);
		
		return true;
	}
}

class DocTag {
	enum Type {
		PARAM,
		RETURN
	}	
	
	@type : Type;
	@name : String;
	@desc : String;

	New(name : String, desc : String) {
		@type := Type->PARAM;
		@name := name;
		@desc := desc;
	}

	New(desc : String) {
		@type := Type->RETURN;
		@name := "";
		@desc := desc;
	}
	
	method : public : GetType() ~ DocTag->Type {
		return @type;
	}

	method : public : GetName() ~ String {
		return @name;
	}

	method : public : GetDesc() ~ String {
		return @desc;
	}
}

class DocType {
	enum Type {
		BYTE,
		CHAR,
		INT,
		FLOAT,
		CLASS,
		FUNC,
		NIL
	}
	
	@name : String;
	@type_name : String;		
	@type : DocType->Type;
	
	New(name : String, type_name : String, type : Type) {
		@name := name;
		@type_name := type_name;
		@type := type;
	}

	method : public : GetName() ~ String {
		return @name;
	}

	method : public : GetTypeName() ~ String {
		return @type_name;
	}

	method : public : GetType() ~ Type {
		return @type;
	}
}

