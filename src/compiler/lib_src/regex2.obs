use Struct;

bundle RegEx2 {
	enum ExpressionType := -100 {
		CHAR,
		ANY,
		OPTIONAL,
		ZERO_MORE,
		ONE_MORE,		
		OR,
		CHAR_CLASS,
		CHAR_CLASS_RANGE,
		SUB_EXPR
	}
	
	class Expression {
		@type : ExpressionType;
		@value : Char;
		@char_class : String;
		@expression : Expression;
		@left : Vector;
		@right : Vector;
		
		New() {
			@type := ExpressionType->ANY;
		}

		New(value : Char) {
			@type := ExpressionType->CHAR;
			@value := value;
		}

		New(char_class : String, is_range : Bool) {
			if(is_range) {
				@type := ExpressionType->CHAR_CLASS_RANGE;
			}
			else {
				@type := ExpressionType->CHAR_CLASS;
			};
			@char_class := char_class;
		}
		
		New(left : Vector) {
			@type := ExpressionType->SUB_EXPR;
			@left := left;
		}
		
		New(type : ExpressionType, expression : Expression) {
			@type := type;
			@expression := expression;
		}

		New(left : Vector, right : Vector) {
			@type := ExpressionType->OR;
			@left := left;
			@right := right;
		}

		method : public : GetType() ~ ExpressionType {
			return @type;
		}
		
		method : public : GetValue() ~ Char {
			return @value;
		}

		method : public : GetClass() ~ String {
			return @char_class;
		}
		
		method : public : GetLeft() ~ Vector {
			return @left;
		}
		
		method : public : GetRight() ~ Vector {
			return @right;
		}
		
		method : public : GetExpression() ~ Expression {
			return @expression;
		}
	}
	
	enum EvalType := -200 {
		MATCH,
		MATCH_NEXT,
		NO_MATCH
	}
	
	class RegExParser {
		# parser
		@tokens : Char[];
		@token_pos : Int;
		@token : Char;		
		@expressions : Vector;
		@error : String;
		# evaluation
		@input : Char[];
		@input_pos : Int;
		@input_char : Char;
		@is_exact : Bool;
		
		New(input : String) {
			@tokens := input->ToCharArray();
			@expressions := Vector->New();
			
			Parse();
		}

		# ---------- Evaluation ----------
		
		method : public : MatchExact(input : String) ~ Bool {
			return Evaluate(input, 0) > -1 & @is_exact;
		}

		method : public : Match(input : String) ~ String {
			return Match(input, 0);
		}
		
		method : public : Match(input : String, offset : Int) ~ String {
			right := Evaluate(input, 0);
			if(right > -1) {
				return input->SubString(offset, right - offset);
			};
			
			return "";
		}

		method : public : FindFirst(input : String) ~ String {
			matches := Vector->New();
			left := 0;
			while(left < input->Size()) {
				right := Evaluate(input, left);
				if(right > -1) {
IO.Console->Print("\t=== left=")->Print(left)->Print(", right=")->Print(right)->PrintLine(" ===");
					return input->SubString(left, right - left);
				};
				left += 1;
			};

			return "";
		}

		method : public : Find(input : String) ~ Vector {
			matches := Vector->New();
			left := 0;
			while(left < input->Size()) {
				right := Evaluate(input, left);
				if(right > -1) {
IO.Console->Print("\t=== left=")->Print(left)->Print(", right=")->Print(right)->PrintLine(" ===");
					matches->AddBack(input->SubString(left, right - left));
					left += right - 1;
				};
				left += 1;
			};

			return matches;
		}

		method : public : Evaluate(input : String, offset : Int) ~ Int {
			if(@error <> Nil) {
				@error->PrintLine();
				return -1;
			};

			if(offset >= input->Size()) {
				return -1;
			};
			
			@is_exact := false;
			@input_pos := offset;			
			@input := input->ToCharArray();
			NextInput();
			
			# evaluate and check final position
			match := Evaluate(@expressions);

IO.Console->Print("@@@ pos=")->Print(@input_pos)->Print(", match=")
	->Print(match)->Print(", end=")->PrintLine(@input_char);

			if(match & @input_char <> '\0') {
				return @input_pos - 1;
			}
			else if(match) {
				@is_exact := true;
				return @input_pos;
			};
			
			return -1;
		}
		
		method : private : NextInput() ~ Nil {
			if(@input_pos < @input->Size()) {
				@input_char := @input[@input_pos];
				@input_pos += 1;
			}
			else {
				@input_char := '\0';
			};
		}	
		
		method : private : Evaluate(expressions : Vector) ~ Bool {
			each(i : expressions) {
				result := Evaluate(expressions->Get(i)->As(Expression));
				if(result = false) {
					return false;
				};
			};
			
			return true;
		}
		
		method : private : Evaluate(expression : Expression) ~ Bool {
			if(expression <> Nil) {
				if(expression->GetType() = ExpressionType->SUB_EXPR) {
					Evaluate(expression->GetLeft());
				}
				else {
					select(expression->GetType()) {
						label ExpressionType->CHAR: {
IO.Console->Print("[### matching: expr='")->Print(expression->GetValue())
	->Print("', input='")->Print(@input_char)->PrintLine("' ###]");
							if(expression->GetValue() = @input_char) {
								NextInput();
								return true;
							};
						}
		
						label ExpressionType->ANY: {
"[### any ###]"->PrintLine();
							NextInput();
							return true;							
						}

						label ExpressionType->CHAR_CLASS: {
							char_class := expression->GetClass();
IO.Console->Print("[### matching range: expr='")->Print(expression->GetClass())
	->Print("', input='")->Print(@input_char)->PrintLine("' ###]");
							found := false;
							for(i := 0; i < char_class->Size() & found = false; i += 1;) {
								if(char_class->Get(i) = @input_char) {
									found := true;
									NextInput();
								};
							};
							
							return found;
						}

						label ExpressionType->CHAR_CLASS_RANGE: {
							char_class := expression->GetClass();
IO.Console->Print("[### matching class: expr='")->Print(char_class)
	->Print("', input='")->Print(@input_char)->PrintLine("' ###]");
							if(char_class->Get(0) <= @input_char & char_class->Get(2) >= @input_char) {
								NextInput();
								return true;
							};
						}

						label ExpressionType->OR: {
"[### or ###]"->PrintLine();
							last := @input_pos - 1;
							if(Evaluate(expression->GetLeft()) = false) {
								@input_pos := last;
								NextInput();
								if(Evaluate(expression->GetRight())) {
									return true;
								};
							};

							return true;
						}
		
						label ExpressionType->ZERO_MORE: {
							test := expression->GetExpression()->GetValue();
IO.Console->Print("[### zero_or_more: expr='")->Print(test)
	->Print("', input='")->Print(@input_char)->PrintLine("' ###]");
							while(test = @input_char) {
IO.Console->Print("\t matching: expr='")->Print(test)
	->Print("', input='")->Print(@input_char)->PrintLine("' ###]");
								NextInput();
							};

							return true;
						}
		
						label ExpressionType->ONE_MORE: {
							test := expression->GetExpression()->GetValue();
IO.Console->Print("[### one_or_more: expr='")->Print(expression->GetValue())
	->Print("', input='")->Print(@input_char)->PrintLine("' ###]");
							if(test = @input_char) {
								NextInput();
								while(test = @input_char) {
IO.Console->Print("\t matching: expr='")->Print(test)
	->Print("', input='")->Print(@input_char)->PrintLine("' ###]");
								NextInput();
								};
	
								return true;
							};
						}
						
						label ExpressionType->OPTIONAL: {
							test := expression->GetExpression()->GetValue();
IO.Console->Print("[### optional: expr='")->Print(test)
	->Print("', input='")->Print(@input_char)->PrintLine("' ###]");
							if(test = @input_char) {
IO.Console->Print("\t matching: expr='")->Print(test)
	->Print("', input='")->Print(@input_char)->PrintLine("' ###]");
								NextInput();
							};

							return true;
						}
					};
				};
			};

			return false;
		}

		#----------- Parsing -----------

		method : private : NextToken() ~ Nil {
			if(@token_pos < @tokens->Size()) {
				@token := @tokens[@token_pos];
				@token_pos += 1;
			}
			else {
				@token := '\0';
			};
		}	

		method : private : Parse() ~ Nil {
			NextToken();
			Binary();
		}	
		
		method : private : Binary() ~ Nil {
			while(@token <> '\0' & @token <> ')' & @token <> '|') {
				Unary();		
				if(@error <> Nil) {
					return;
				};
			};
			
			if(@token = '|') {
				NextToken();				
				
				left := @expressions;
				@expressions := Vector->New();
				
				Binary();
				if(@error <> Nil) {
					return;
				};
				
				right := @expressions;
				@expressions := Vector->New();
				
				@expressions->AddBack(Expression->New(left, right));
IO.Console->PrintLine("match: or_match");					
			};			
		}

		method : private : Unary() ~ Nil {
			Value();
			if(@error <> Nil) {
				return;
			};

			if(@token = '*') {
				if(@expressions->Size() > 0) {
					expression := @expressions->RemoveBack()->As(Expression);
					@expressions->AddBack(Expression->New(ExpressionType->ZERO_MORE, expression));
					NextToken();
IO.Console->PrintLine("match: zero_or_more");					
				}
				else {
					@error := "missing left-hand side operand";
					return;
				};
			}
			else if(@token = '+') {
				if(@expressions->Size() > 0) {
					expression := @expressions->RemoveBack()->As(Expression);
					@expressions->AddBack(Expression->New(ExpressionType->ONE_MORE, expression));
					NextToken();
IO.Console->PrintLine("match: one_or_more");					
				}
				else {
					@error := "missing left-hand side operand";
					return;
				};
			}
			else if(@token = '?') {
				if(@expressions->Size() > 0) {
					expression := @expressions->RemoveBack()->As(Expression);
					@expressions->AddBack(Expression->New(ExpressionType->OPTIONAL, expression));
					NextToken();
IO.Console->PrintLine("match: optional");					
				}
				else {
					@error := "missing left-hand side operand";
					return;
				};
			};
		}

		method : private : Value() ~ Nil {
			if(@token = '(') {
				Parentheses();				
			}
			else if(@token = '[') {
				CharacterClass();
			}
			else if(@token = '\\') {
				EscapeCharacter();
"char: escaped"->PrintLine();
			}			
			else if(@token = '.') {
				@expressions->AddBack(Expression->New());
"match: any"->PrintLine();			
				NextToken();
			}			
			else {
				@expressions->AddBack(Expression->New(@token));
IO.Console->Print("match: char=")->PrintLine(@token);				
				NextToken();
			};
		}
		
		method : private : CharacterClass() ~ Nil {	
			NextToken();

			if(@token->IsChar() = false & @token->IsDigit() = false) {
				@error := "invalid character class";
			};
			
			start := @token_pos - 1;
			hyphen_count := 0;
			while(@token <> '\0' & @token <> ']' & (@token = '-' | @token->IsChar() | @token->IsDigit())) { 
				if(@token = '-') {
					hyphen_count += 1;
				};
				NextToken();
			};
			end := @token_pos - 1;
			char_class := String->New(@tokens, start, end - start);

			if(@token <> ']') {
				@error := "expected ')'";
				return;
			};
			NextToken();
			
			if(hyphen_count > 1) {
				@error := "invalid character class";
			};

			is_range := false;
			if(hyphen_count = 1) {
				if(char_class->Size() <> 3) {
					@error := "invalid character class";
					return; # to avoid an issue with next statement
				};
				
				if(char_class->Get(0) >= char_class->Get(2)) {
					@error := "invalid character class";
				};

				is_range := true;				
			};

IO.Console->Print("match range: class=")->PrintLine(char_class);
			
			@expressions->AddBack(Expression->New(char_class, is_range));
		}

		method : private : EscapeCharacter() ~ Nil {
			NextToken();
			select(@token) {
				label '|': 
				label '.': 
				label '?': 
				label '*': 
				label '+': {
					@expressions->AddBack(Expression->New(@token));
					NextToken();
				}

				label 'n': {
					@expressions->AddBack(Expression->New('\n'));
					NextToken();
				}

				label 'r': {
					@expressions->AddBack(Expression->New('\r'));
					NextToken();
				}

				label 'b': {
					@expressions->AddBack(Expression->New('\b'));
					NextToken();
				}

				label 't': {
					@expressions->AddBack(Expression->New('\t'));
					NextToken();
				}

				label '\\': {
					@expressions->AddBack(Expression->New('\\'));
					NextToken();
				}
			};
		}

		method : private : Parentheses() ~ Nil {
			NextToken();
			
			left := @expressions;
			@expressions := Vector->New();
				
			Binary();
			if(@error <> Nil) {
				return;
			};
		
			if(@token <> ')') {
				@error := "expected ')'";
				return;
			};
			NextToken();
			
			# combine lists			
			left->AddBack(@expressions);
			@expressions := left;
			
IO.Console->PrintLine("parentheses");		
		}	

		# ---------- Debug ----------
		
		method : private : PrintSpace(depth : Int) ~ Nil {
			for(i := 0; i < depth; i += 1;) {
				"  "->Print();
			};
		}

		method : private : Dump(expression : Expression, depth : Int) ~ Nil {
			if(expression <> Nil) {
				if(expression->GetType() = ExpressionType->SUB_EXPR) {
					Dump(expression->GetLeft(), depth + 1);
				}
				else {
					
					
					select(expression->GetType()) {
						label ExpressionType->CHAR: {
							PrintSpace(depth); IO.Console->Print("[=== match: token='")
								->Print(expression->GetValue())->PrintLine("' ===]");
						}
		
						label ExpressionType->ANY: {
							PrintSpace(depth); "[=== match: any"->PrintLine();
						}

						label ExpressionType->OR: {
							PrintSpace(depth); "[=== or ===]"->PrintLine();
							
							PrintSpace(depth + 2); "[--- left ---]"->PrintLine();
							Dump(expression->GetLeft(), depth + 3);
							
							PrintSpace(depth + 2); "[--- right ---]"->PrintLine();
							Dump(expression->GetRight(), depth + 3);
						}
		
						label ExpressionType->ZERO_MORE: {
							PrintSpace(depth); "[=== zero or more ===]"->PrintLine();
							Dump(expression->GetExpression(), depth + 2);
						}
		
						label ExpressionType->ONE_MORE: {
							PrintSpace(depth); "[=== one or more ===]"->PrintLine();
							Dump(expression->GetExpression(), depth + 2);
						}

						label ExpressionType->OPTIONAL: {
							PrintSpace(depth); "[=== optional ===]"->PrintLine();
							Dump(expression->GetExpression(), depth + 2);
						}
					};
				};
			};	
		}
		
		method : private : Dump(expressions : Vector, depth : Int) ~ Nil {
			each(i : expressions) {
				Dump(expressions->Get(i)->As(Expression), depth);
			};
		}

		method : private : Dump() ~ Nil {
			"-------------------"->PrintLine();
			if(@error <> Nil) {
				@error->PrintLine();
			}
			else {
				Dump(@expressions, -1);
			};
		}

	}
}
