use Struct;

bundle Default {
	enum ExpressionType := -100 {
		CHAR_MATCH,
		ANY_MATCH,
		OR_MATCH,
		ZERO_MORE,
		ONE_MORE,
		LIST
	}
	
	class Expression {
		@type : ExpressionType;
		@value : Char;
		@expression : Expression;
		@left : Vector;
		@right : Vector;
		
		New() {
			@type := ExpressionType->ANY_MATCH;
		}

		New(value : Char) {
			@type := ExpressionType->CHAR_MATCH;
			@value := value;
		}
		
		New(left : Vector) {
			@type := ExpressionType->LIST;
			@left := left;
		}
		
		New(type : ExpressionType, expression : Expression) {
			@type := type;
			@expression := expression;
		}

		New(left : Vector, right : Vector) {
			@type := ExpressionType->OR_MATCH;
			@left := left;
			@right := right;
		}

		method : public : GetType() ~ ExpressionType {
			return @type;
		}
		
		method : public : GetValue() ~ Char {
			return @value;
		}
		
		method : public : GetLeft() ~ Vector {
			return @left;
		}
		
		method : public : GetRight() ~ Vector {
			return @right;
		}
		
		method : public : GetExpression() ~ Expression {
			return @expression;
		}
	}
	
	class RegExParser {
		@tokens : Char[];
		@token : Char;
		@token_pos : Int;
		@expressions : Vector;
		@error : String;
		
		New(input : String) {
			@tokens := input->ToCharArray();
			@expressions := Vector->New();
			
			NextToken();
			Parse();
Dump();
		}
		
		method : private : PrintSpace(depth : Int) ~ Nil {
			for(i := 0; i < depth; i += 1;) {
				"  "->Print();
			};
		}

		method : private : Dump(expression : Expression, depth : Int) ~ Nil {
			if(expression <> Nil) {
				if(expression->GetType() = ExpressionType->LIST) {
					Dump(expression->GetLeft(), depth + 1);
				}
				else {
					PrintSpace(depth);
					
					select(expression->GetType()) {
						label ExpressionType->CHAR_MATCH: {
							IO.Console->Print("[=== match: token='")->Print(expression->GetValue())->PrintLine("' ===]");
						}
		
						label ExpressionType->ANY_MATCH: {
							"[=== match: any"->PrintLine();
						}
		
						label ExpressionType->OR_MATCH: {
							"[=== or ===]"->PrintLine();
							
							PrintSpace(depth + 1); "[--- left ---]"->PrintLine();
							Dump(expression->GetLeft(), depth + 2);
							
							PrintSpace(depth + 1); "[--- right ---]"->PrintLine();
							Dump(expression->GetRight(), depth + 2);
						}
		
						label ExpressionType->ZERO_MORE: {
							"[=== zero or more ===]"->PrintLine();
							Dump(expression->GetExpression(), depth + 2);
						}
		
						label ExpressionType->ONE_MORE: {
							"[=== one or more ===]"->PrintLine();
							Dump(expression->GetExpression(), depth + 2);
						}
					};
				};
			};	
		}
		
		method : private : Dump() ~ Nil {
			if(@error <> Nil) {
				@error->PrintLine();
			}
			else {
				Dump(@expressions, -1);
			};
		}
		
		method : private : Dump(expressions : Vector, depth : Int) ~ Nil {
			each(i : expressions) {
				Dump(expressions->Get(i)->As(Expression), depth);
			};
		}
		
		method : private : Compile() ~ Nil {
			if(@expressions->Size() > 0) {

			};
		}

		method : private : NextToken() ~ Nil {
			if(@token_pos < @tokens->Size()) {
				@token := @tokens[@token_pos];
				@token_pos += 1;
			}
			else {
				@token := '\0';
			};
		}

		method : private : Parse() ~ Nil {
			Binary();
		}		
		
		method : private : Binary() ~ Nil {
			while(@token <> '\0' & @token <> ')' & @token <> '|') {
				Unary();		
				if(@error <> Nil) {
					return;
				};
			};
			
			if(@token = '|') {
"--- DIVIDE ---"->PrintLine();			
				NextToken();				
				
				left := @expressions;
				@expressions := Vector->New();
				
				Binary();
				if(@error <> Nil) {
					return;
				};
				
				right := @expressions;
				@expressions := Vector->New();
				
				@expressions->AddBack(Expression->New(left, right));
IO.Console->PrintLine("match: or_match");					
			};			
		}

		method : private : Unary() ~ Nil {
			Value();
			if(@error <> Nil) {
				return;
			};

			if(@token = '*') {
				if(@expressions->Size() > 0) {
					expression := @expressions->RemoveBack()->As(Expression);
					@expressions->AddBack(Expression->New(ExpressionType->ZERO_MORE, expression));
					NextToken();
IO.Console->PrintLine("match: zero_or_more");					
				}
				else {
					@error := "missing left-hand side operand";
					return;
				};
			}
			else if(@token = '+') {
				if(@expressions->Size() > 0) {
					expression := @expressions->RemoveBack()->As(Expression);
					@expressions->AddBack(Expression->New(ExpressionType->ONE_MORE, expression));
					NextToken();
IO.Console->PrintLine("match: one_or_more");					
				}
				else {
					@error := "missing left-hand side operand";
					return;
				};
			};
		}

		method : private : Value() ~ Nil {
			if(@token->IsChar() | @token->IsDigit()) {
				@expressions->AddBack(Expression->New(@token));
IO.Console->Print("match: char=")->PrintLine(@token);				
				NextToken();
			}
			else if(@token = '(') {
				Parentheses();				
			}
			else if(@token = '.') {
				@expressions->AddBack(Expression->New());
"match: any"->PrintLine();			
				NextToken();
			}			
			else {
				@error := "invalid value";
				return;
			};
		}
	
		method : private : Parentheses() ~ Nil {
			NextToken();					
			Binary();
			if(@error <> Nil) {
				return;
			};
		
			if(@token <> ')') {
				@error := "expected ')'";
				return;
			};
			NextToken();
			
			expressions := @expressions;
			@expressions := Vector->New();
			@expressions->AddBack(Expression->New(expressions));
			
IO.Console->PrintLine("parentheses");		
		}	
	}
	
	class Test {
		function : Main(args : String[]) ~ Nil {
			if(args->Size() = 1) {
				regex := RegExParser->New(args[0]);
			};
		}
	}


}
