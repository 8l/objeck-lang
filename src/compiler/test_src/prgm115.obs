use IO;
use Struct;

#~
Basic NFA regex engine
R. Hollines 

Based on an implementation by Russ Cox.
Copyright (c) 2007 Russ Cox.
Can be distributed under the MIT license, see bottom of file.
~#

bundle RegEx {
	class PostFix {
		@input : String;
		@frag_stack : Stack;
		@listid : Int;
		@l1 : NFAList;
		@l2 : NFAList;
		
		New(input : String) {	
			@input := input;
			@frag_stack := Stack->New();
			
input->PrintLine();
			postfix := RegEx2Postfix(@input);
			if(postfix <> Nil) {
postfix->PrintLine();
				x := Postfix2Nfa(postfix);
				Dump(x,0);
				Evaluate(x, "b")->PrintLine();
			};
		}
		
		#~
		 ...
		 ...
		~#
		method : public : RegEx2Postfix(input : String) ~ String {
			output := "";
			parens := IntStack->New();
			alt_count := 0;
			atom_count := 0;
			
			each(i : input) {
				char := input->Get(i);
				select(char){
					label '(': {
						if(atom_count > 1) {
							atom_count -= 1;
							output->Append('.');
						};
						
						parens->Push(alt_count);
						parens->Push(atom_count);
						
						alt_count := 0;
						atom_count := 0;
					}
					
					label '|': {
						if(atom_count = 0) {
							return Nil;
						};
							
						atom_count -= 1;
						while(atom_count > 0) {
							output->Append('.');
							atom_count -= 1;
						};
						
						alt_count += 1;
					}
						
					label ')': {
						if(parens->Size() = 0) {
							return Nil;
						};
						
						if(atom_count = 0) {
							return Nil;
						};
						
						atom_count -= 1;
						while(atom_count > 0) {
							output->Append('.');
							atom_count -= 1;
						};
						
						while(alt_count > 0) {
							output->Append('|');
							alt_count -= 1;
						};
						
						atom_count := parens->Pop();
						alt_count := parens->Pop();
						
						atom_count += 1;
					}
					
					label '*':
					label '+':
					label '?': {
						if(atom_count = 0) {
							return Nil;
						};
						
						output->Append(char);
					}
					
					other: {
						if(atom_count > 1) {
							atom_count -= 1;
							output->Append('.');
						};
						
						output->Append(char);
						atom_count += 1;
					}
				};
			};
			
			if(parens->Size() <> 0) {
				return Nil;
			};
			
			atom_count -= 1;
			while(atom_count > 0) {
				output->Append('.');
				atom_count -= 1;
			};
			
			while(alt_count > 0) {
				output->Append('|');
				alt_count -= 1;
			};
			
			return output;
		}
		
		#~
		 ...
		 ...
		~#
		method : Postfix2Nfa(postfix : String) ~ State {
			if(postfix = Nil) {
				return Nil;
			};
		
			each(i : postfix) {
				char := postfix->Get(i);
				select(char) {
					other: {
						s := State->New(char->As(Int), Nil->As(State), Nil->As(State));
						f := Fragment->New(s, CreateList(s));
						@frag_stack->Push(f->As(Base));
					}
					
					# catenate
					label '.': {
						e2 := @frag_stack->Pop()->As(Fragment);
						e1 := @frag_stack->Pop()->As(Fragment);
						
						Patch(e1->GetOuts(), e2->GetStart());
						f := Fragment->New(e1->GetStart(), e2->GetOuts());
						@frag_stack->Push(f->As(Base));
					}

					# alternate
					label '|':{
						e2 := @frag_stack->Pop()->As(Fragment);
						e1 := @frag_stack->Pop()->As(Fragment);
						
						s := State->New(Operation->SPLIT->As(Int),
							e1->GetStart(), e2->GetStart());
						f := Fragment->New(s, Append(e1->GetOuts(), e2->GetOuts()));
						@frag_stack->Push(f->As(Base));
					}
						
					# zero or one
					label '?':	{
						e := @frag_stack->Pop()->As(Fragment);
						s := State->New(Operation->SPLIT->As(Int),
							e->GetStart(), Nil->As(State));
						f := Fragment->New(s, Append(e->GetOuts(), CreateList(s)));
						@frag_stack->Push(f->As(Base));
					}
					
					# zero or more
					label '*': {
						e := @frag_stack->Pop()->As(Fragment);
						s := State->New(Operation->SPLIT->As(Int),
							e->GetStart(), Nil->As(State));
						Patch(e->GetOuts(), s);
						f := Fragment->New(s, CreateList(s));
						@frag_stack->Push(f->As(Base));
					}
					
#~				
					# one or more
					label '+': {
						e = pop();
						s = State->New(Split, e.start, Nil);
						patch(e.out, s);
						push(frag(e.start, list1(s->out1)));
					}
~#					
				};
			};

			# ensure all states have been processed
			e := @frag_stack->Pop()->As(Fragment);
			if(@frag_stack->Size() <> 0) {
				return Nil;
			};
			
			# set matching state
			m := State->New(Operation->MATCH->As(Int),
				Nil->As(State), Nil->As(State));
			Patch(e->GetOuts(), m);
			
			return e->GetStart();
		}
		
		method : CreateList(state : State) ~ List {
			list := List->New();
			list->AddBack(state->As(Compare));
			return list;
		}
		
		method : Patch(list : List, state : State) ~ Nil {
			list->Rewind();
			while(list->IsEnd() <> true) {
				s := list->Get()->As(State);
"== p =="->PrintLine();				
				s->SetOut(state);				
				list->Next();
			};
		}
		
		method : Append(left : List, right : List) ~ List {
			right->Rewind();
			while(right->IsEnd() <> true) {
				left->AddBack(right->Get());
				right->Next();
			};
			
			return left;
		}
		
		
		
		
		
		
		
		method : AddState(l : NFAList, s : State) ~ Nil {
			if(s = Nil | s->GetLastList() = @listid) {
				return;
			};
				
			s->SetLastList(@listid);
			if(s->GetValue() = Operation->SPLIT->As(Int)) {
				x := s->GetOut() = Nil ? 0 : 1;
				y := s->GetOut1() = Nil ? 0 : 1;
				
Console->Print("split l=")->Print(x)->Print(", r=")->PrintLine(y);			
				AddState(l, s->GetOut());
				AddState(l, s->GetOut1());
				return;
			};
			l->AddState(s);
		}
		
		# Compute initial state list
		method : StartList(start : State, l : NFAList) ~ NFAList {
			@listid += 1;
			AddState(l, start);
			return l;
		}
		
		method : Step(clist : NFAList, c : Int, nlist : NFAList) ~ Nil {
			@listid += 1;
			nlist->Reset();
			states := clist->GetStates();
			each(i : states) {
				s := states->Get(i)->As(State);
			
Console->Print("test l=")->Print(s->GetValue())->Print(", r=")->PrintLine(c);			


				value := s->GetValue();
				if(value = Operation->MATCH->As(Int) | value = c) {
					AddState(nlist, s->GetOut());
				};
			};
		}
		
		method : IsMatch(l : NFAList) ~ Bool {
			states := l->GetStates();
			each(i : states) {
				s := states->Get(i)->As(State);				
				if(s->GetValue() = Operation->MATCH->As(Int)) {
					return true;
				};
			};
			
			return false;
		}
		
		#~
		Run NFA to determine whether it matches s. 
		~#
		method : Evaluate(start : State, s : String) ~ Bool {
			@l1 := NFAList->New();
			clist := StartList(start, @l1);
			
			@l2 := NFAList->New();
			nlist := @l2;

			each(i : s) {
"= 99 ="->PrintLine();			
				c := s->Get(i);
				Step(clist, c->As(Int), nlist);
				t := clist; 
				clist := nlist; 
				nlist := t;
			};
						
			return IsMatch(clist);
		}

		
		
		
		
		
		
		function : Dump(s : State, l : Int) ~ Nil {
			if(s <> Nil) {
Console->Print("state=")->Print(s->GetValue())->Print(", level=")->PrintLine(l);
				Dump(s->GetOut(), l + 1);
				Dump(s->GetOut1(), l + 1);
			};
		}
				
		function : Main(args : String[]) ~ Nil {
			postfix := PostFix->New(args[0]);
		}
	}
	
	class NFAList {
		@states : Vector;

		New() {
			@states := Vector->New();
		}
		
		method : public : AddState(s : State) ~ Nil {
			@states->AddBack(s->As(Base));
		}	
		
		method : public : Reset() ~ Nil {
			@states := Vector->New();
		}	
		
		method : public : GetStates() ~ Vector {
			return @states;
		}
	}
	
	enum Operation := 256 {
		MATCH,
		SPLIT
	}
	
	class Fragment {
		@start : State;
		@outs : List;
		
		New(start : State, outs : List) {
			@start := start;
			@outs := outs;
		}
		
		method : public : GetStart() ~ State {
			return @start;
		}
		
		method : public : SetStart(start : State) ~ Nil {
			@start := start;
		}
		
		method : public : GetOuts() ~ List {
			return @outs;
		}
		
		method : public : SetOuts(outs : List) ~ Nil {
			@outs := outs;
		}
	}
	
	class State from BasicCompare {
		@value : Int;
		@out : State;
		@out1 : State;
		@lastlist : Int;
	
		New(value : Int, out: State, out1 : State) {
			Parent();
			
			@value := value;
			@out := out;
			@out1 := out1;
		}
		
		method : public : GetValue() ~ Int {
			return @value;
		}
		
		method : public : GetOut() ~ State {
			return @out;
		}
		
		method : public : SetOut(out : State) ~ Nil {
			@out := out;
		}
		
		method : public : GetOut1() ~ State {
			return @out1;
		}
		
		method : public : SetOut1(out1 : State) ~ Nil {
			@out1 := out1;
		}
		
		method : public : GetLastList() ~ Int {
			return @lastlist;
		}
		
		method : public : SetLastList(lastlist : Int) ~ Nil {
			@lastlist := lastlist;
		}
	}
}

#~
Permission is hereby granted, free of charge, to any person
obtaining a copy of this software and associated
documentation files (the "Software"), to deal in the
Software without restriction, including without limitation
the rights to use, copy, modify, merge, publish, distribute,
sublicense, and/or sell copies of the Software, and to
permit persons to whom the Software is furnished to do so,
subject to the following conditions:

The above copyright notice and this permission notice shall
be included in all copies or substantial portions of the
Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY
KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE
WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR
PURPOSE AND NONINFRINGEMENT.  IN NO EVENT SHALL THE AUTHORS
OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR
OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR
OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
~#