use IO;
use Struct;

bundle Default {
	#~
	# Tag PageParser
	~#
	class PageParser  {
		@index : Int;
		@page : String;
		@page_chars : Char[];
		@cur_char : Char;
		@nxt_char : Char;
		@nxt_nxt_char : Char;
		@code : String;
		@tag_stack : Stack;
		@blocks : Vector;

		New(page : String) {
			if(page <> Nil) {
				@page := page;
				@page_chars := page->ToCharArray();
				@index := -1;
				@code := String->New();
				@tag_stack := Stack->New();
				NextChar();
				@blocks := Vector->New();
			};
		}

		# get next char, look ahead 3 chars
		method : NextChar() ~ Nil {
			@index += 1;
			if(@index < @page_chars->Size()) {
				@cur_char := @page_chars[@index];
				if(@index + 1 < @page_chars->Size()) {
					@nxt_char := @page_chars[@index + 1];
					if(@index + 2 < @page_chars->Size()) {
						@nxt_nxt_char := @page_chars[@index + 2];
					}
					else {	
						@nxt_nxt_char := '\0';
					};
				}
				else {	
					@nxt_char := '\0';
				};
			}
			else {
				@cur_char := '\0';
			};
		}

		# parse page
		method : public : Parse() ~ Nil {
			if(@page_chars = Nil) {
				return;
			};

			tag_start := false;
			html_block := String->New();
			while(@cur_char <> '\0') {
				# end tag
				if(@cur_char = '<' & @nxt_char = '/' & @nxt_nxt_char = '#') {
					EndTag();
					html_block := String->New();
					tag_start := false;
				}
				# start tag
				else if(@cur_char = '<' & @nxt_char = '#') {
					StartTag(html_block);
					tag_start := true;
				}
				# unary end tag
				else if(tag_start & @cur_char = '/' & @nxt_char = '>') {
					UnaryEndTag();
					html_block := String->New();
					tag_start := false;
				}
				# html markup
				else {
					if(tag_start <> true) {
						html_block->Append(@cur_char);
					};
					NextChar();	
				};
			};
			@blocks->AddBack(Block->New(BlockType->HTML, html_block));

			if(@tag_stack->Size() = 0) {
				each(i : @blocks) {
					block := @blocks->Get(i)->As(Block);
					if(block->GetType() = BlockType->HTML) {
						Console->Print("html='")->Print(block->GetText())->PrintLine("'");
					}
					else {
						Console->Print("code='")->Print(block->GetText())->PrintLine("'");
					};
				};
				"OK"->PrintLine();
			}
			else {
				"mismatched tags"->PrintLine();
			};
		}

		# ending unary tag
		method : UnaryEndTag() ~ Nil {
			NextChar();	NextChar();	
			top_tag := @tag_stack->Top()->As(Tag);
			if(top_tag = Nil) {
				"Expected starting tag..."->PrintLine();
				Runtime->Exit(1);
			};
			@tag_stack->Pop();
			text := @page->SubString(top_tag->GetPosition(), @index - top_tag->GetPosition() - 2);
			@blocks->AddBack(Block->New(BlockType->CODE, text));
Console->Print("\tending unary: text='")->Print(text)->PrintLine("'");
		}
	
		# ending tag
		method : EndTag() ~ Nil {
			NextChar();	NextChar();	NextChar();	
			start := @index;
			while(@cur_char <> '>') {
				NextChar();	
			};
			end := @index;

			top_tag := @tag_stack->Top()->As(Tag);
			name := @page->SubString(start, end -start);
			if(top_tag = Nil | top_tag->GetName()->Equals(name) = false) {
				"Expected starting tag..."->PrintLine();
				Runtime->Exit(1);
			};
			@tag_stack->Pop();

			text := @page->SubString(top_tag->GetPosition(), start - top_tag->GetPosition() - 3);
			@blocks->AddBack(Block->New(BlockType->CODE, text));
Console->Print("\tending: text='")->Print(text)->PrintLine("'");
			NextChar();	
		}
		
		# start tag
		method : StartTag(html_block : String) ~ Nil {
			# add html block
			@blocks->AddBack(Block->New(BlockType->HTML, html_block));
			
			NextChar();	NextChar();	
			start := @index;
			while(@cur_char <> ' ' & @cur_char <> '>') {
				NextChar();	
			};
			end := @index;
			name := @page->SubString(start, end -start);
			NextChar();
			@tag_stack->Push(Tag->New(@index, name));
Console->Print("starting tag: '")->Print(name)->PrintLine("'");
		}

		function : Main(args : String[]) ~ Nil {
			if(args->Size() = 1) {
				parser := PageParser->New(FileReader->ReadFile(args[0]));
				parser->Parse();
			};
		}	
	}

	#~
	# Tag class
	~#
	class Tag {
		@name : String;
		@pos : Int;
		
		New(pos : Int, name : String) {	
			@pos := pos;
			@name := name;
		}
		
		method : public : GetName() ~ String {
			return @name;
		}
		
		method : public : GetPosition() ~ Int {
			return @pos;
		}
	}

	#~
	# Block class
	~#
	enum BlockType {
		HTML,
		CODE
	}
	
	class Block {
		@block_type : BlockType;
		@text : String;

		New(block_type : BlockType, text : String) {
			@block_type := block_type;
			@text := text;
		}

		method : public : GetType() ~ BlockType {
			return @block_type;
		}

		method : public : GetText() ~ String {
			return @text;
		}	
	}
}
